%require "3.2"
%language "c++"

%define api.namespace {voila::parser}
%define api.parser.class {Parser}
%define api.value.type variant
%define api.token.constructor
%define parse.assert
%define parse.error verbose

%{
	#include "ASTNodes.hpp"
	#include "Program.hpp"
%}

%debug
%defines
%output "src/voila_parser.cpp"
%defines "include/voila_parser.hpp"
%locations
%define api.location.file "../include/location.hpp"

%code requires {
	namespace voila::lexer {
		class Lexer; // Generated by reflex with namespace=voila lexer=Lexer lex=yylex
	}
}
%parse-param { voila::lexer::Lexer& lexer } {voila::Program &out}// Construct parser object with lexer and output

%code {
	#include <iostream>
	#include <cstdlib>
	#include <fstream>
	#include <string>
	#include <cinttypes>
	#include <vector>
	#include "voila_lexer.hpp"
	#undef yylex
	#define yylex lexer.lex // Within bison's parse() we should invoke lexer.yylex(), not the global yylex()
}

%define api.token.prefix {TOK_}

/* special chars */
%token BAR "|"
%token COLON ";"
%token COMMA ","
%token LPAREN "("
%token RPAREN ")"
%token LBRACE "{"
%token RBRACE "}"
%token LBRACKET "["
%token RBRACKET "]"

/* literals */
%token TRUE "true"
%token FALSE "false"

/* special functions */
%token LOOP "loop"
%token FUNCTION "function definition"
%token EMIT "emit"
%token MAIN "main function"

/* comparison */
%token EQ "equals" 
%token NEQ "not equals"
%token LE "less"
%token LEQ "less equals"
%token GE "greater"
%token GEQ "greater equals"

/* arithmetic */
%token ADD "addition"
%token SUB "subtraction"
%token MUL "multiplication"
%token DIV "division"
%token MOD "modulo"

/* logical */
%token AND "and"
%token OR "or"
%token NOT "not"

/* TODO: unary operators */
%token HASH "hash"
%token SELECT "matching selection"

/* binary operators */
%token GATHER "gather"
%token READ "read"
/* ternary operators */
%token SCATTER "scatter"
%token WRITE "write"
/* aggregates */
%token AGGR "aggregation"
%token SUM "sum"
%token CNT "count"
%token MIN "min"
%token MAX "max"
%token AVG "average"
/* TODO hash table ops */

%nonassoc ASSIGN "assignment"

%token <intmax_t> INT "integer"
%token <double> FLT "decimal"
%token <std::string> ID "identifier"
%token <std::string> STR "string literal"

%nterm <std::vector<ast::Statement>> stmts;
%nterm <std::vector<ast::Expression>> expr_list;
%nterm <std::vector<ast::Fun>> program;
%nterm <ast::Statement> stmt;
%nterm <ast::Statement> function_call;
%nterm <ast::Expression> expr;
%nterm <ast::Fun *> func;
%nterm <ast::Main *> main;
%nterm <ast::Expression> constant;
%nterm <ast::Expression> pred;
%nterm <ast::Expression> pred_expr;
%nterm <ast::Expression> var;
%nterm <std::vector<ast::Expression>> var_list;

%nterm <ast::Statement> effect;
%nterm <ast::Expression> arithmetic;
%nterm <ast::Expression> comparison;
%nterm <ast::Expression> selection;
%nterm <ast::Expression> logical;
%nterm <ast::Expression> read_op;
%start program

%%
program: 
	%empty { }
	| program func { out.add_func($2); }
	| program main { out.add_func($2); } //TODO: main function is singleton

func: FUNCTION ID LPAREN var_list RPAREN LBRACE stmts RBRACE { $$ = new ast::Fun(@1+@6,$2, $4, $7); out.infer_type(*$$);}

main: MAIN LPAREN var_list RPAREN LBRACE stmts RBRACE { $$ = new ast::Main(@1+@5,$3, $6); out.infer_type(*$$); }

stmts:
	%empty { }
	| stmts stmt { $$ = $1; $$.push_back($2); }

stmt: expr COLON { $$ = ast::Statement::make<StatementWrapper>(@1,$1); }
    | var ASSIGN function_call COLON { $$ = ast::Statement::make<Assign>(@2,$1, $3); out.infer_type($$); }
	| var ASSIGN expr COLON { $$ = ast::Statement::make<Assign>(@2,$1, ast::Statement::make<StatementWrapper>(@3,$3)); out.infer_type($$); }
	| LOOP pred LBRACE stmts RBRACE { $$ = ast::Statement::make<Loop>(@1+@2,$2, $4); out.infer_type($$); }
	| EMIT expr COLON { $$ = ast::Statement::make<Emit>(@1,$2); out.infer_type($$); }
	| effect COLON { $$ = $1; }
	| effect COLON pred { $$ = $1; $$.set_predicate($3); }
	| function_call COLON { $$ = $1; }

function_call: ID LPAREN var_list RPAREN { $$ = ast::Statement::make<FunctionCall>(@1+@4,$1, $3); out.infer_type($$); }

var: ID {$$ = out.has_var($1) ? ast::Expression::make<Ref>(@1, out.get_var($1)) : ast::Expression::make<Variable>(@1, $1); if ($$.is_variable()){ out.add_var($$);}; out.infer_type($$); };

	/* aggregate ( result_store, variable with predicate as aggregation filter, vector_to_aggregate) */
effect :
	AGGR LPAREN SUM COMMA expr COMMA expr RPAREN { $$ = Statement::make<AggrSum>(@1+@8,$5, $7); } /* maybe we restrict the expressions to more specialized predicates or tuple get in the parser to safe some correctness check effort later on */
	| AGGR LPAREN CNT COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<AggrCnt>(@1+@8,$5, $7); }
	| AGGR LPAREN AVG COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<AggrAvg>(@1+@8,$5, $7); }
	| AGGR LPAREN MIN COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<AggrMin>(@1+@8,$5, $7); }
	| AGGR LPAREN MAX COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<AggrMax>(@1+@8,$5, $7); }
	| SCATTER LPAREN expr COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<Scatter>(@1+@8,$3, $5, $7); } /* dest, idxs with pred, src */
	| WRITE LPAREN expr COMMA expr COMMA expr RPAREN { $$ = ast::Statement::make<Write>(@1+@8,$3, $5, $7); } /* src, dest, start_idx */

pred: BAR pred_expr { $$ = Expression::make<Predicate>(@2,$2); out.infer_type($$); }

selection:
	SELECT LPAREN expr COMMA expr RPAREN { $$ = ast::Expression::make<Selection>(@1+@4,$3, $5); }

expr: 
	constant { $$ = $1; }
	| var
	| var LBRACKET INT RBRACKET { $$ = ast::Expression::make<TupleGet>(@2+@4,$1, $3); assert($1.is_reference()); }
	| LPAREN expr_list RPAREN { $$ = ast::Expression::make<TupleCreate>(@1+@3,$2); } /* recursive tuples do not look like a good idea */
	| expr pred { $$ = $1; $$.set_predicate($2); out.infer_type($$); }
	| arithmetic {$$ = $1; out.infer_type($$); }
	| comparison {$$ = $1; out.infer_type($$); }
	| logical {$$ = $1; }
	| read_op {$$ = $1; out.infer_type($$); }
	| selection { $$ = $1; out.infer_type($$); }

/* TODO: is this correct/complete? */
pred_expr:
    ID { $$ = ast::Expression::make<Ref>(@1,out.get_var($1)); }
    | comparison {$$ = $1; }
    | logical {$$ = $1; }

constant:
	TRUE { $$ = ast::Expression::make<BooleanConst>(@1,true); out.infer_type($$); }
	| FALSE { $$ = ast::Expression::make<BooleanConst>(@1, false); out.infer_type($$); }
	| INT { $$ = ast::Expression::make<IntConst>(@1,$1); out.infer_type($$); }
	| FLT { $$ = ast::Expression::make<FltConst>(@1,$1); out.infer_type($$); }
	| STR { $$ = ast::Expression::make<StrConst>(@1,$1); out.infer_type($$); }

arithmetic :
	ADD LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Add>(@1+@6,$3, $5); }
	| SUB LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Sub>(@1+@6,$3, $5); }
	| MUL LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Mul>(@1+@6,$3, $5); }
	| DIV LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Div>(@1+@6,$3, $5); }
	| MOD LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Mod>(@1+@6,$3, $5); }

comparison : 
	EQ LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Eq>(@1+@6,$3, $5); }
	| NEQ LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Neq>(@1+@6,$3, $5); }
	| LE LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Le>(@1+@6,$3, $5); }
	| LEQ LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Leq>(@1+@6,$3, $5); }
	| GE LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Ge>(@1+@6,$3, $5); }
	| GEQ LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Geq>(@1+@6,$3, $5); }

logical:
	AND LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<And>(@1+@6,$3, $5); out.infer_type($$);}
 	| OR LPAREN expr COMMA expr RPAREN {$$ = ast::Expression::make<Or>(@1+@6,$3, $5); out.infer_type($$);}
 	| NOT LPAREN expr RPAREN {$$ = ast::Expression::make<Not>(@1+@4,$3); out.infer_type($$);}

read_op:
	GATHER LPAREN expr COMMA expr RPAREN { $$ = ast::Expression::make<Gather>(@1+@6,$3, $5); out.infer_type($$);}
	| READ LPAREN expr COMMA expr RPAREN { $$ = ast::Expression::make<Read>(@1+@6,$3, $5); out.infer_type($$);}

expr_list: 
	expr { $$ = std::vector<ast::Expression>(); $$.push_back($1);}
	| expr_list COMMA expr { $$ = $1; $$.push_back($3); }

var_list:
    var {$$ = std::vector<ast::Expression>(); $$.push_back($1);};
    | var_list COMMA var {$$ = $1; $$.push_back($3); };

%%


void voila::parser::Parser::error(const location& loc, const std::string& msg)
{
	std::cerr <<
	loc << ": " << msg << std::endl;
	if (lexer.size() == 0) // if token is unknown (no match)
		lexer.matcher().winput(); // skip character
}